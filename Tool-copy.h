BEGIN PGM TOOL-COPY MM 
; Program to copy a tool to a new number
; Written by Martin Bjoersberg
; Tested in a DMG DMU 60eVo with a PH150
; Controll Heidenhain TNC640
;
;
FN 0 : Q1 = +10 ; Select tool to copy
FN 0: Q2 = +20 ; Select new tool number
FN 0: Q3 = +1; 1 if you want to reset the old tool values, 0 to keep it.

; Copy the data from the old tool
FN 18: SYSREAD Q1601 = ID50 NR1 IDXQ1 ; L
FN 18: SYSREAD Q1602 = ID50 NR2 IDXQ1 ; R
FN 18: SYSREAD Q1603 = ID50 NR3 IDXQ1 ; R2
FN 18: SYSREAD Q1604 = ID50 NR4 IDXQ1 ; DL
FN 18: SYSREAD Q1605 = ID50 NR5 IDXQ1 ; DR
FN 18: SYSREAD Q1609 = ID50 NR9 IDXQ1 ; TIME1
FN 18: SYSREAD Q1610 = ID50 NR10 IDXQ1 ; TIME2
FN 18: SYSREAD Q1611 = ID50 NR11 IDXQ1 ; CUR_TIME
FN 18: SYSREAD Q1615 = ID50 NR15 IDXQ1 ; CUT
FN 18: SYSREAD Q1616 = ID50 NR16 IDXQ1 ; LTOL
FN 18: SYSREAD Q1617 = ID50 NR17 IDXQ1 ; RTOL
FN 18: SYSREAD Q1619 = ID50 NR19 IDXQ1 ; R-OFFS
FN 18: SYSREAD Q1620 = ID50 NR20 IDXQ1 ; L-OFFS
FN 18: SYSREAD Q1621 = ID50 NR21 IDXQ1 ; LBREAK
FN 18: SYSREAD Q1622 = ID50 NR22 IDXQ1 ; RBREAK
FN 18: SYSREAD Q1636 = ID50 NR36 IDXQ1 ; Tool type


; Write the data to the new tool
FN 17: SYSWRITE ID50 NR1 IDXQ2 = Q1601 ; L
FN 17: SYSWRITE ID50 NR2 IDXQ2 = Q1602 ; R
FN 17: SYSWRITE ID50 NR3 IDXQ2 = Q1603 ; R2
FN 17: SYSWRITE ID50 NR4 IDXQ2 = Q1604 ; DL
FN 17: SYSWRITE ID50 NR5 IDXQ2 = Q1605 ; DR
FN 17: SYSWRITE ID50 NR9 IDXQ2 = Q1609 ; TIME1
FN 17: SYSWRITE ID50 NR10 IDXQ2 = Q1610 ; TIME2
FN 17: SYSWRITE ID50 NR11 IDXQ2 = Q1611 ; CUR_TIME
FN 17: SYSWRITE ID50 NR15 IDXQ2 = Q1615 ; CUT
FN 17: SYSWRITE ID50 NR16 IDXQ2 = Q1616 ; LTOL
FN 17: SYSWRITE ID50 NR17 IDXQ2 = Q1617 ; RTOL
FN 17: SYSWRITE ID50 NR19 IDXQ2 = Q1619 ; R-OFFS
FN 17: SYSWRITE ID50 NR20 IDXQ2 = Q1620 ; L-OFFS
FN 17: SYSWRITE ID50 NR21 IDXQ2 = Q1621 ; LBREAK
FN 17: SYSWRITE ID50 NR22 IDXQ2 = Q1622 ; RBREAK
FN 17: SYSWRITE ID50 NR36 IDXQ2 = Q1636 ; Tool type


FN 9: IF Q3 EQU +0 GOTO LBL 1

; Reset the old tool
FN 17: SYSWRITE ID50 NR1 IDXQ1 = +0 ; L
FN 17: SYSWRITE ID50 NR2 IDXQ1 = +0 ; R
FN 17: SYSWRITE ID50 NR3 IDXQ1 = +0 ; R2
FN 17: SYSWRITE ID50 NR4 IDXQ1 = +0 ; DL
FN 17: SYSWRITE ID50 NR5 IDXQ1 = +0 ; DR
FN 17: SYSWRITE ID50 NR9 IDXQ1 = +0 ; TIME1
FN 17: SYSWRITE ID50 NR10 IDXQ1 = +0 ; TIME2
FN 17: SYSWRITE ID50 NR11 IDXQ1 = +0 ; CUR_TIME
FN 17: SYSWRITE ID50 NR15 IDXQ1 = +2 ; CUT
FN 17: SYSWRITE ID50 NR16 IDXQ1 = +0.1 ; LTOL
FN 17: SYSWRITE ID50 NR17 IDXQ1 = +0.1 ; RTOL
FN 17: SYSWRITE ID50 NR19 IDXQ1 = +0 ; R-OFFS
FN 17: SYSWRITE ID50 NR20 IDXQ1 = +0 ; L-OFFS
FN 17: SYSWRITE ID50 NR21 IDXQ1 = +0 ; LBREAK
FN 17: SYSWRITE ID50 NR22 IDXQ1 = +0 ; RBREAK
FN 17: SYSWRITE ID50 NR36 IDXQ1 = +0 ; Tool type

LBL 1 ; End
M30
END PGM TOOL-COPY MM